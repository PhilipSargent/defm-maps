
10REM****************
20REM*              *
30REM*    UNPLOT    *
40REM*              *
50REM****************
60REM
70REM PHILIP SARGENT
80REM
90REM  UNPLT#31 27 OCTOBER 1982 
100REM
110REM VERSIONS 1-16 FOR PET.
120REM
130HOME:INVERSE:PRINT:PRINT
140PRINT"HI THERE"
150PRINT
160PRINT"THIS IS FREDDIE,"
170PRINT
180PRINT"YOUR DESK-TOP COMPUTER,"
190PRINT
200PRINT"AND I'M FEELING JUST GREAT, GUYS,"
210PRINT
220PRINT"AND I JUST KNOW I'M GOING TO GET"
230PRINT
240PRINT"A BUNDLE OF KICKS OUT OF ANY GRAPH"
250PRINT
260PRINT"YOU CARE TO RUN THRU ME"
270PRINT
280FORI=1TO2500:NEXT:HOME
290PRINT:PRINT:PRINT
300PRINT"LET'S GET WITH IT GANG!"
310NORMAL
320DIMXP(800),YP(800),XB(800),YB(800)
330DIML(800)
340E$=CHR$(4):REMCNTRL/D
350C$=","
360FORI=1TO1000:NEXT:HOME
370PRINT:PRINT:PRINT
380PRINT"DATA FROM DISC OR TABLET ?"
390PRINT:PRINT"(ANSWER D OR T)"
400GETF2$
410IFF2$="D"THEN1960
420IFF2$<>"T"THEN390
430HOME
440PRINT:PRINT:PRINT
450PRINT"IF YOU WISH TO PROCESS THE DATA"
460PRINT:PRINT"RATHER THAN MERELY SCALE IT,"
470PRINT:PRINT"THEN INSERT A BIT OF PROGRAM"
480PRINT:PRINT"BETWEEN LINES 6000 AND 7000,"
490PRINT:PRINT"THE UNSCALED POINTS ARE XP & YP"
500PRINT:PRINT"AND ARE RETURNED AS XB & YB"
510GOSUB4090
520REM INITIALISE TABLET
530PRINTE$
540PRINTCHR$(4);"PR#5"
550PRINT"TEXT 1,SCALE 1,XOFF 1536,YOFF 1536,F,BEFORE,P,C"
560PRINTE$;"PR#0"
570HOME:PRINT:PRINT:PRINT
580INPUT"TITLE: ";T$
590PRINT:PRINT
600PRINT"IS THE X-AXIS LOGARITHMIC ?"
610PRINT:PRINT"(ANSWER Y OR N)"
620GETQ$:IFQ$=""THEN620
630IFQ$="Y"THEN660
640IFQ$="N"THEN670
650GOTO610
660L1=1:GOTO680
670L1=0:GOTO680
680PRINT:PRINT"IS THE Y-AXIS LOGARITHMIC ?"
690PRINT:PRINT"(ANSWER Y OR N)
700GETQ$:IFQ$=""THEN700
710IFQ$="Y"THEN740
720IFQ$="N"THEN750
730GOTO690
740L2=1:GOTO760
750L2=0:GOTO760
760PRINT:PRINT
770INPUT"X-AXIS LIMITS ON PLOT: ";XL,XH
780PRINT
790IFL1=0THEN850
800IFXL=0THEN830
810IFXH=0THEN830
820GOTO850
830PRINT"LOG SCALE - NO ZEROS PLEASE."
840PRINT:GOTO770
850INPUT"Y-AXIS LIMITS ON PLOT: ";YL,YH
860IFL2=0THEN920
870IFYL=0THEN900
880IFYH=0THEN900
890GOTO920
900PRINT:PRINT"LOG SCALE - NO ZEROS PLEASE."
910PRINT:GOTO850
920PRINT:PRINT
930INPUT"SCALE MULTIPLIER FOR X ";BX
940PRINT
950INPUT"SCALE MULTIPLIER FOR Y ";BY
960GOSUB4090
970HOME:PRINT:PRINT
980PRINT"DO YOU WANT TO VERIFY EACH POINT"
990PRINT:PRINT"BY PRESSING THE SPACE-BAR ?"
1000PRINT:PRINT"(ANSWER Y OR N)"
1010GETW$
1020IFW$="N"THEN1050
1030IFW$<>"Y"THEN1000
1040F1$="T"
1050HOME
1060PRINT:PRINT"SET X-ORIGIN ON TABLET"
1070GOSUB4390
1080X0=ZX:Y1=ZY
1090PRINT:PRINT"SET X-AXIS LIMIT ON TABLET"
1100GOSUB4390
1110XMAX=ZX:YCHK=ZY
1120PRINT:PRINT"SET Y-ORIGIN ON TABLET"
1130GOSUB4390
1140X1=ZX:Y0=ZY
1150PRINT:PRINT"SET Y-AXIS LIMIT ON TABLET"
1160GOSUB4390
1170YMAX=ZY:XCHK=ZX
1180E1=YMAX-Y0:E2=X1-XCHK
1190E3=YCHK-Y1:E4=XMAX-X0
1200IFE2=0THEN1230
1210REM ?"E2,2%E4 ";E2,E4*0.02
1220IFABS(E2)>ABS(0.02*E4)THEN1270
1230IFE3=0THEN1470
1240REM ?"E3,2%E1 ";E3,E1*0.02
1250IFABS(E3)>ABS(0.02*E1)THEN1370
1260GOTO1470
1270HOME:INVERSE:PRINT:PRINT:PRINT
1280PRINT"LIFE,"
1290FORI=1TO750:NEXT
1300PRINT:PRINT:PRINT"DON'T TALK TO ME ABOUT LIFE..."
1310FORI=1TO2000:NEXT:HOME:NORMAL
1320PRINT:PRINT:PRINT"Y-AXIS IS TILTED MORE THAN"
1330PRINT:PRINT"2% OF THE X-AXIS"
1340PRINT:PRINT"SET AXES AGAIN.":PRINT:PRINT
1350GOSUB4090
1360GOTO1050
1370HOME:INVERSE:PRINT:PRINT:PRINT
1380PRINT"LIFE,"
1390FORI=1TO750:NEXT
1400PRINT:PRINT:PRINT"DON'T TALK TO ME ABOUT LIFE..."
1410FORI=1TO2000:NEXT:HOME:NORMAL
1420PRINT:PRINT:PRINT"X-AXIS IS TILTED MORE THAN"
1430PRINT:PRINT"2% OF THE Y-AXIS"
1440PRINT:PRINT"SET AXES AGAIN.":PRINT:PRINT
1450GOSUB4090
1460GOTO1050
1470INVERSE:HOME:PRINT:PRINT
1480PRINT"YYYYUUMMMM..."
1490FORI=1TO1000:NEXT:NORMAL
1500HOME
1510XS=(XH-XL)/E4:YS=(YH-YL)/E1
1520IFL1=0THEN1540
1530K1=LOG(XH/XL)/E4
1540IFL2=0THEN1560
1550K2=LOG(YH/YL)/E1
1560PRINT:PRINT:PRINT"GIVE AN INTEGER (1-6) TO BE USED"
1570PRINT:PRINT"TO SET THE PLOTTING SYMBOL
1580PRINT:PRINT"(THIS CAN BE CHANGED AT ANY TIME)"
1590PRINT:PRINT": ":GETL
1600L=INT(L)
1610IFL>6THEN1590
1620IFL<1THEN1590
1630GOSUB4200:REMGETSMENU
1640HOME:PRINT:PRINT
1650PRINT"IF YOUR DATA POINTS DON'T MAKE SENSE,"
1660PRINT:PRINT"TYPE 'R' AND START AGAIN."
1670PRINT:PRINT:PRINT"TO REGAIN MENU OF AVAILABLE KEYSTROKES,"
1680PRINT:PRINT"TYPE  H"
1690GOSUB4090
1700HOME:PRINT:PRINT:PRINT
1710PRINT"ENTER POINTS WITH THE 'PEN' ON"
1720PRINT:PRINT"THE TABLET"
1730IFF1$="T"THENPRINT:PRINT"PRESS THE SPACE-BAR AFTER EACH POINT"
1735IFF4$<>"T"THENJK=1
1740J=JK
1750GOSUB4390:REMGETSPOINTFROMTABLET
1760RX=ZX-X0
1770RY=ZY-Y0
1780IFL1>=1THEN1800
1790XP=RX*XS+XL:GOTO1810
1800XP=EXP(RX*K1)*XL
1810IFL2>=1THEN1830
1820YP=RY*YS+YL:GOTO1840
1830YP=EXP(RY*K2)*YL
1840XB=XP*BX:YB=YP*BY
1850GOSUB6000
1860XP(J)=XP:XB(J)=XB
1870YP(J)=YP:YB(J)=YB
1880L(J)=L
1890PRINTJ;"  ";XP;"  ";YP"  ";L
1900JT=J
1910J=J+1
1920GOTO1750
1930REM
1940REM--------------------------------
1950REM
1960REM READ DATA FROM DISC
1970REM
1980PRINT
1990PRINT:INPUT"NAME OF DATA FILE: ";N2$
2000GOSUB4090
2010PRINTE$
2020PRINTE$;"OPEN ";N2$
2030PRINTE$;"READ ";N2$
2040INPUTT$
2050INPUTBX:INPUTBY
2060INPUTJT
2070FORJ=1TOJT
2080INPUTJ,L(J),XP(J),YP(J),XB(J),YB(J)
2090NEXTJ
2100PRINTE$;"CLOSE ";N2$
2110HOME:PRINT:PRINT:PRINT
2120PRINT"DATA FILE ";N2$;" READ IN OK."
2125T$=N2$
2130REM
2140REM END OF DISC READ
2150REM
2160REM-------------------------------
2170REM
2180REM PROCESSING BIT
2190REM
2200REM
2210GOSUB5320:REMSUMMARY
2220GOSUB4090:REMWAIT
2230GOSUB2640:REMPRINTOUT
2240GOSUB4780:REMRESCALE
2250GOSUB5320:REMSUMMARY
2260GOSUB4090:REMWAIT
2280GOSUB3100:REMDISCSAVE
2290GOSUB4090:REMWAIT
2295GOSUB3410:REMPLOTOUT
2300REM--------------------------------
2310REM
2320REM WINDUP
2330REM
2340HOME:PRINT:PRINT
2350PRINT"ANOTHER SET OF DATA ?"
2360PRINT:PRINT"(ANSWER Y OR N)
2370GETW$:IFW$=""THEN2370
2380IFW$="N"THEN2490
2390IFW$<>"Y"THEN2360
2400PRINT:PRINT:PRINT"DATA FROM DISC OR TABLET ?"
2410PRINT:PRINT"(ANSWER D OR T)":PRINT:GETW$
2420IFW$="D"THEN1990
2430IFW$<>"T"THEN2410
2440PRINT:PRINT:PRINT"SAME PLOT ?"
2450GETW$:IFW$=""THEN2450
2460IFW$<>"Y"THEN570
2470PRINT:INPUT"TITLE: ";T$
2480PRINT:GOTO1470
2490HOME:PRINT:PRINT:PRINT
2500PRINT"DON'T FORGET TO UNSTICK YOUR PLOT"
2510PRINT:PRINT"AND TO CLEAN THE TABLET:"
2520PRINT:PRINT"ETHANOL OR PROPAN-2-OL IS OK,"
2530PRINT:PRINT"BUT DON'T USE ACETONE."
2540INVERSE
2550PRINT:PRINT:PRINT"THANKYOU FOR YOUR CUSTOM,"
2560PRINT:PRINT"HAVE A NICE DAY NOW."
2570NORMAL
2580END
2590REM
2600REM--------------------------------
2610REM
2620REM PRINT OUT RESULTS
2630REM
2640PRINT:PRINT"PRINT OUT RESULTS"
2650A1$="PLOT X"
2660A2$="PLOT Y"
2670A3$="NEW X"
2680A4$="NEW Y"
2690A5$="    "
2700PRINT:PRINT"(ANSWER Y OR N)":GETW$
2710IFW$="Y"THEN2740
2720IFW$<>"N"THEN2700
2730RETURN
2740PRINT:PRINT:PRINT"HARDCOPY OR SCREEN ?"
2750PRINT:PRINT"(ANSWER H OR S) ":GETW$
2760IFW$="S"THEN2950
2770IFW$<>"H"THEN2750
2780HOME:PRINTT$
2790PRINTE$;"PR#1"
2800PRINT:PRINTT$
2810PRINT:PRINT"X MULTIPLIER = ";BX
2820PRINT:PRINT"Y MULTIPLIER = ";BY
2830PRINT:LIST6001-6999
2840PRINT:PRINT"    ",A1$;A5$,A2$;A5$,A3$;A5$,A4$
2850PRINT
2860FORJ=1TOJT
2870J$=STR$(J)
2880IFJ<10THENJ$=J$+" "
2890IFJ<100THENJ$=J$+" "
2900J$=J$+" "+STR$(L(J))+" "
2910PRINTJ$,XP(J),YP(J),XB(J),YB(J)
2920NEXTJ
2930PRINTE$;"PR#0"
2940RETURN
2950PRINT:PRINT"    ",A1$;A5$,A2$;A5$,A3$;A5$,A4$
2960PRINT
2970FORJ=1TOJT
2980J$=STR$(J):IFJ<10THENJ$=J$+" "
2990IFJ<100THENJ$=J$+" "
3000PRINTJ$;XP(J);" ";YP(J);" ";XB(J);" ";YB(J);" ";L(J)
3010NEXTJ
3020RETURN
3030REM
3040REM END OF DATA PRINTOUT
3050REM
3060REM--------------------------------
3070REM
3080REM BEGIN DISC SAVE
3090REM
3100PRINT:PRINT:PRINT"SAVE DATA ON DISC ?"
3110PRINT:PRINT"(ANSWER Y OR N)":GETW$
3120IFW$="Y"THEN3150
3130IFW$<>"N"THEN3110
3140RETURN
3150HOME:PRINT:PRINTT$:PRINT
3160PRINT:INPUT"NAME FOR NEW DATA FILE: ";N$
3170PRINT:PRINT"COPY NOW ?"
3180GOSUB4090
3190PRINTE$
3200PRINTE$;"OPEN ";N$
3210PRINTE$;"DELETE ";N$
3220PRINTE$;"OPEN ";N$
3230PRINTE$;"WRITE ";N$
3240PRINTT$
3250PRINTBX:PRINTBY
3260PRINTJT
3270FORJ=1TOJT
3280PRINTJ;C$;L(J);C$;XP(J);C$;YP(J);C$;XB(J);C$;YB(J)
3290NEXTJ
3300PRINTE$;"CLOSE ";N$
3310PRINTE$;"LOCK ";N$
3320PRINT:PRINTN$;" NOW FILED & LOCKED."
3330RETURN
3340REM
3350REM END OF DISC SAVE
3360REM
3370REM--------------------------------
3380REM
3390REM PLOTOUT DATA
3400REM
3410PRINT:PRINT:PRINT
3420PRINT"PLOT SCALED/PROCESSED DATA ON DIGIPLOT ?"
3430PRINT:PRINT"(ANSWER Y OR N) ":GETW$
3440IFW$="N"THENRETURN
3450IFW$<>"Y"THEN3430
3460IFF3$<>"T"THEN3550
3470PRINT:PRINT:PRINT"SAME AXES AS BEFORE ?"
3480PRINT:PRINT"(ANSWER Y OR N)":PRINT:GETW$
3490IFW$="Y"THEN3930
3500IFW$<>"N"THEN3480
3510INVERSE:PRINT:PRINT:PRINT"(NOTE,"
3520PRINT:PRINT" CAN ONLY PLOT LINEAR/LINEAR AXES AS YET,"
3530PRINT:HTAB12:PRINT"SORRY.)"
3540NORMAL
3550PRINT
3560PRINT:INPUT"LIMITS FOR X-AXIS: ";C1,C2
3570PRINT:INPUT"LIMITS FOR Y-AXIS: ";Y1,Y2
3580PRINT:PRINT:PRINT"PLOT AXES ?"
3590PRINT:PRINT"(ANSWER Y OR N)":GETW$
3600IFW$="N"THEN3930
3610IFW$<>"Y"THEN3590
3620PRINT:PRINT"X-LENGTH = ";C2-C1
3630PRINT:INPUT"NUMBER OF X-AXIS SUBDIVISIONS: ";KH
3640PRINT:PRINT:PRINT"Y-LENGTH = ";Y2-Y1
3650PRINT:INPUT"NUMBER OF Y-AXIS SUBDIVISIONS: ";KV
3660PRINT:PRINT:INPUT"TITLE FOR X AXIS: ";AX$
3670PRINT:INPUT"TITLE FOR Y AXIS: ";AY$
3680PRINT:PRINT:PRINTT$
3690PRINT:INPUT"OVERALL TITLE: ";S$
3700GOSUB9700:X=120:Y=120:GOSUB9100
3710X=1:Y=3200/KH:R=KH:GOSUB8500
3720TEXT
3730X=0:Y=2200/KV:R=KV:GOSUB8500
3740GOSUB9700:X=120:Y=120:GOSUB9100
3750X=0:Y=2200/KV:R=KV:GOSUB8500
3760X=1:Y=3200/KH:R=KH:GOSUB8500
3770X$=S$:X9$="S":Y=7:GOSUB8000
3780X9$="Q":Y=0:GOSUB8000
3790X=1000:Y=2355:GOSUB9100:GOSUB9400
3800X$=AX$:X9$="S":Y=5:GOSUB8000
3810X9$="Q":Y=0:GOSUB8000
3820X=1000:Y=10:GOSUB9100:GOSUB9400:X9$="S":Y=3:GOSUB8000
3830X$=STR$(C1):X=120:Y=75:GOSUB9100:GOSUB9400
3840X$=STR$(C2):X=3260:GOSUB9100:GOSUB9400
3850X9$="Q":Y=1:GOSUB8000
3860X=60:Y=1000:GOSUB9100
3870X9$="S":Y=5:GOSUB8000
3880X$=AY$:GOSUB9400
3890X9$="S":Y=3:GOSUB8000
3900X$=STR$(Y1):X=90:Y=60:GOSUB9100:GOSUB9400
3910X$=STR$(Y2):Y=2260:GOSUB9100:GOSUB9400
3920F3$="T"
3930HOME:PRINT:PRINT
3940PRINT:PRINT"PLOT POINTS NOW ?":GOSUB4090
3950DEFFNSX(Z)=(3200*(Z-C1)/(C2-C1)+120)
3960DEFFNSY(Z)=(2200*(Z-Y1)/(Y2-Y1)+120)
3961PRINT:PRINT"PLEASE GIVE SIZE FOR MARKS,"
3962PRINT:INPUT"RANGE 0-15 (DEFAULT 5): ";MK
3963IFMK<0THEN3962
3964IFMK>15THEN3962
3970X9$="S":Y=MK:GOSUB8000
3980FORJ=1TOJT
3990X=FNSX(XB(J)):Y=FNSY(YB(J))
4000GOSUB9100
4010Y=L(J):X9$="N"
4020GOSUB8000
4030NEXTJ
4040GOSUB9700
4050RETURN
4060REM--------------------------
4070REM
4080REM WAIT ROUTINE
4090INVERSE
4100VTAB23:HTAB37:PRINT"GO ?"
4110GETW$:IFW$=""THEN4110
4120VTAB23:HTAB37:PRINT" OK "
4130NORMAL
4140RETURN
4150REM
4160REM--------------------------------
4170REM
4180REM MENU
4190REM
4200HOME:PRINT:PRINT"AFTER ANY POINT TYPE:"
4210PRINT:PRINT"S  START AGAIN FROM SCRATCH"
4220PRINT:PRINT"R  RESET PLOT AXES AND RE-ENTER POINTS"
4230PRINT:PRINT"T  BEGIN ALL DATA POINTS AGAIN"
4240PRINT:PRINT"D  DELETE LAST DATA POINT,"
4250PRINT"   (DD FOR LAST TWO, DDD FOR"
4260PRINT"    LAST THREE, ETC.)"
4270PRINT:PRINT"C  CHANGE POINT LABEL STRING"
4280PRINT:PRINT"Q  QUIT"
4285PRINT:PRINT"N NEXT GRAPH"
4290PRINT:PRINT"E  END OF DATA"
4300GOSUB4090
4310RETURN
4320REM
4330REM END OF MENU
4340REM
4350REM--------------------
4360REM
4370REM GETS POINT FROM TABLET
4380REM
4390PRINTE$:GOTO4520
4400A=PEEK(-16384)
4410POKE-16368,0
4420REM GETS KEYBOARD CHARACTER
4430IFA=209THEN2340:REMQ
4440IFA=210THEN1050:REMR
4450IFA=211THEN640:REMS
4460IFA=212THEN1740:REMT
4470IFA=195THEN4660:REMC
4480IFA=196THEN4710:REMD
4490IFA=197THEN2230:REME
4495IFA=206THEN4725:REMN
4500IFA=200THENGOSUB4200:REMH
4510PRINT"NON MENU CHARACTER"
4520PRINTE$;"IN#5":INPUT"";ZX,ZY,Z:PRINTE$;"IN#0"
4530POKE37,22
4540IFZ<0THEN4400
4550Z=ABS(Z)
4560IFZ>10THENPRINT"PEN OFF SCALE"
4570IFZ>=10THENZ=Z-10:REMGETSONESDIGIT
4580IFZ<>2THEN4520
4590PRINTE$;"IN#5":INPUT"";ZX,ZY,Z:PRINTE$;"IN#0"
4600IFZ<>0THEN4590
4610IFF1$="T"THENGETW$
4620RETURN
4630REM
4640REM END OF TABLET GET
4650REM
4660PRINT:PRINT:PRINT"SYMBOL INTEGER (1-6): ":GETL
4670L=INT(L)
4680IFL>6THEN4660
4690IFL<1THEN4660
4700PRINTE$:GOTO4520
4710J=J-1:PRINT"LAST POINT DELETED"
4720GOTO1750
4725JK=J:F4$="T":GOTO590
4730REM
4740REM--------------------------------
4750REM
4760REM RESCALE/REPROCESS
4770REM
4780PRINT:PRINT:PRINT"DO YOU WISH TO RESCALE OR TO"
4790PRINT:PRINT"REPROCESS THE DATA ?"
4800PRINT:PRINT"(ANSWER Y OR N) ":GETW$
4810IFW$="N"THENRETURN
4820IFW$<>"Y"THEN4800
4830HOME:PRINT:PRINT:PRINT
4840PRINT"X MAGNIFICATION IS/WAS ";BX
4850PRINT:PRINT"Y MAGNIFICATION IS/WAS ";BY
4860PRINT:INPUT"NEW X MAG.: ";BX
4870PRINT:INPUT"NEW Y MAG.: ";BY
4880PRINT:PRINT:PRINT"USING CODE IN LINES 6001-6999:"
4890PRINT:LIST6001-6999
4900GOSUB4090
4910PRINT:PRINT:PRINT"CHANGE SYMBOL INTEGERS ?"
4920PRINT:PRINT"(ANSWER Y OR N) ":GETF4$
4930IFF4$="Y"THEN4950
4940IFF4$<>"N"THEN4920
4950GOSUB4090
4960FORJ=1TOJT
4970XP=XP(J):YP=YP(J)
4980XB=XP*BX:YB=YP*BY
4990GOSUB6000
5000XB(J)=XB:YB(J)=YB
5010IFF4$="N"THEN5070
5020PRINTJ;" SYMBOL INTEGER WAS ";L(J);" NOW: "
5030GETL:L=INT(L)
5040IFL>6THEN5020:
5050IFL<1THEN5020
5060L(J)=L
5070NEXTJ
5080PRINT:PRINT"DATA NOW RESCALED."
5090GOSUB2640:REMPRINTOUT
5100RETURN
5110REM
5120REM END OF RESCALE/REPROCESS
5130REM
5140REM--------------------------------
5150REM
5160REM RMXMN
5170REM
5180X6=XB(1):Y6=YB(1)
5190X7=X6:Y7=Y6
5200FORJ=1TOJT
5210IFX6>XB(J)THENX6=XB(J)
5220IFY6>YB(J)THENY6=YB(J)
5230IFX7<XB(J)THENX7=XB(J)
5240IFY7<YB(J)THENY7=YB(J)
5250NEXT
5260RETURN
5270REM
5280REM--------------------------------
5290REM
5300REMSUMMARY
5310REM
5320HOME:PRINT:PRINT:PRINT
5330PRINT"TITLE = ";T$
5340PRINT:PRINT"NUMBER OF DATA POINTS = ";JT
5350PRINT:PRINT"X MULTIPLIER = ";BX
5360PRINT:PRINT"Y MULTIPLIER = ";BY
5370GOSUB5180:DATALIMITS
5380PRINT:PRINT:PRINT
5390PRINT:PRINT"MAX X VALUE = ";X7:PRINT:PRINT"MIN X VALUE = ";X6
5400PRINT:PRINT:PRINT"MAX Y VALUE = ";Y7:PRINT:PRINT"MIN Y VALUE = ";Y6
5410RETURN
5420REM
5430REMEND OF SUMMARY
5440REM
6000REM--------------------------
6001XB=XP*BX
6002YB=YP*BY
7000RETURN
8000PR#2:PRINTX9$;INT(Y):GOTO9800
8500PR#2:PRINT"X";INT(X);"            ,";INT(Y)",";INT(R)
8510GOTO9800
9000PR#2:PRINT"D"INT(X);",            ";INT(Y):GOTO9800
9100PR#2:PRINT"M"INT(X);",            ";INT(Y):GOTO9800
9200PR#2:PRINT"I";INT(X);"            ,";INT(Y):GOTO9800
9300PR#2:PRINT"R";INT(X);"            ,";INT(Y):GOTO9800
9400PR#2:PRINT"P";X$
9700PR#2:PRINT"H"
9800PR#0:RETURN
9990END

